import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id 'org.parchmentmc.writtenbooks' version '0.5.+'
    id 'maven-publish'
    id 'com.apollographql.apollo' version '2.5.9'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

project.group = 'org.parchmentmc'

writtenbooks {
    githubRepo = 'ParchmentMC/Nitwit'
}

repositories {
    mavenCentral()
    maven {
        name 'dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'Minecraft'
        url 'https://libraries.minecraft.net'
    }
}

dependencies {
    implementation 'com.apollographql.apollo:apollo-runtime:2.5.9' // Apollo (GraphQL)
    implementation 'com.apollographql.apollo:apollo-rx3-support:2.5.9' // Apollo support for RxJava3

    implementation 'io.reactivex.rxjava3:rxjava:3.0.13' // RxJava3

    implementation 'org.kohsuke:github-api:1.131' // GitHub API in Java

    implementation 'io.jsonwebtoken:jjwt-api:0.11.2' // JJWT
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2' // impl of JJWT's API
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2' // JJWT Jackson support

    implementation 'com.squareup.okhttp3:okhttp:4.9.0' // OkHttp3

    implementation 'com.mojang:brigadier:1.0.18' // Brigadier

    implementation('net.dv8tion:JDA:4.3.0_289') { // JDA
        exclude module: 'opus-java'
    }

    implementation 'org.spongepowered:configurate-hocon:4.1.1' // Configurate

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    withSourcesJar()
}

apollo {
    customTypeMapping = [
            "DateTime": "java.time.OffsetDateTime",
            "URI"     : "java.net.URI"
    ]
}

jar {
    manifest.attributes([
            'Main-Class': 'org.parchmentmc.nitwit.Main'
    ])
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

sourcesJar {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

def relocateShadowJar = tasks.register("relocateShadowJar", ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "org.parchmentmc.nitwit.shadow"
}

shadowJar {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    includeEmptyDirs = false

    exclude 'META-INF/annotations/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'

    dependsOn relocateShadowJar
}

test {
    useJUnitPlatform()
}

publishing {
    publications.withType(MavenPublication).configureEach {
        pom {
            name = 'Nitwit'
            description = 'GitHub App for managing Pull Requests to the Parchment mappings repository'
            organization {
                name = 'ParchmentMC'
                url = 'https://github.com/ParchmentMC'
            }
            licenses {
                license {
                    name = 'MIT'
                    url = 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    name = "sciwhiz12"
                    email = "sciwhiz12@sciwhiz12.tk"
                }
            }
        }
    }
}
